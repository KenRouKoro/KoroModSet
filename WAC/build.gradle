plugins {
    id 'java'
}
group 'cn.korostudio.mc.wac'
version '1.0.0'

def project_version = version
def forge_version = '1.0.0'
def fabric_version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    compileOnly(project(":HutoolCore"))

    implementation 'org.slf4j:slf4j-api:1.8.0-beta4'

    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'

    testCompileOnly 'org.projectlombok:lombok:1.18.26'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'


}


import org.gradle.jvm.tasks.Jar

import java.nio.file.Files
import java.nio.file.Paths

tasks.register('mergeManifests') {
    doFirst {
        // 清空 external 目录
        delete "$buildDir/external"
    }
    doLast {
        def forgeManifest = "$buildDir/external/Forge/META-INF/MANIFEST.MF"
        def fabricManifest = "$buildDir/external/Fabric/META-INF/MANIFEST.MF"
        def mergedManifest = "$buildDir/external/META-INF/MANIFEST.MF"

        copy {
            from(zipTree("../WACForge/build/libs/wacforge-${forge_version}.jar")) {
                include 'META-INF/MANIFEST.MF'
            }
            into("$buildDir/external/Forge")
        }
        copy {
            from(zipTree("../WACFabric/build/libs/WACFabric-${fabric_version}.jar")) {
                include 'META-INF/MANIFEST.MF'
            }
            into("$buildDir/external/Fabric")
        }

        // 读取所有 MANIFEST.MF 文件的内容
        def forgeLines = Files.readAllLines(Paths.get(forgeManifest))
        def fabricLines = Files.readAllLines(Paths.get(fabricManifest))

        // 合并多个 MANIFEST.MF 文件的内容，去掉重复行并在合适的地方换行
        def manifestContent = new LinkedHashSet<>()
        manifestContent.addAll(forgeLines)
        manifestContent.addAll(fabricLines)

        // 将合并后的内容写入新的 MANIFEST.MF 文件，并在文件末尾添加换行符
        Files.createDirectories(Paths.get("$buildDir/external/META-INF"))
        def manifestContentWithoutEmptyLines = manifestContent.findAll { it != "" }
        Files.write(Paths.get(mergedManifest), (manifestContentWithoutEmptyLines.join("\n") + "\n").getBytes())

        // 提取 Forge、Fabric 文件夹内除了 META-INF/MANIFEST.MF 之外的其他内容
        copy {
            from(zipTree("../WACForge/build/libs/wacforge-${forge_version}.jar")) {
                exclude 'META-INF/MANIFEST.MF'
            }
            into("$buildDir/external")
        }
        copy {
            from(zipTree("../WACFabric/build/libs/WACFabric-${fabric_version}.jar")) {
                exclude 'META-INF/MANIFEST.MF'
            }
            into("$buildDir/external")
        }
    }
}


tasks.register('fatjar', Jar) {
    dependsOn(mergeManifests)
    from("$buildDir/external")
    from(sourceSets.main.output)
    exclude("Forge/**")
    exclude("Fabric/**")
    manifest {
        from("$buildDir/external/META-INF/MANIFEST.MF")
    }
    archiveFileName.set("WAC-${project_version}-For-Forge-Fabric.jar")
}

test {
    useJUnitPlatform()
}